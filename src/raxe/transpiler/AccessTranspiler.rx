-- vim: set ft=rb:

require "raxe/tools/StringHandle"

class self :: Transpiler

def new()
end

def tokens() : Array(:String:)
  return [
    "{", "}", "[", "]", "(", ")", "@",
    "//", "/*", "*/", "\"",
    "var", "function", "public", "private", "module", "new",
  ]
end

def transpile(handle : StringHandle)
  def count = -1
  def isPrivate = false
  def isStatic = false

  while(handle.nextToken())
    if(handle.safeis("module"))
      handle.remove()
      handle.insert("class")
      handle.increment()
      isStatic = true
    elsif(handle.is("\""))
      handle.increment()

      while(handle.nextToken())
        if(handle.is("\"") && (handle.content.charAt(handle.position -1) != "\\" ||
            (handle.content.charAt(handle.position -1) == "\\" && handle.content.charAt(handle.position -2) == "\\")))
          break
        end

        handle.increment()
      end

      handle.increment()
    elsif(handle.is("//") || handle.is("@"))
      handle.increment()
      handle.next("\n")
      handle.increment()
    elsif(handle.is("/*"))
      handle.increment()
      handle.next("*/")
      handle.increment()
    elsif(handle.is("[") || handle.is("{"))
      count = count + 1
      handle.increment()
    elsif(handle.is("]") || handle.is("}"))
      count = count - 1
      handle.increment()
    elsif(handle.safeis("public") || handle.safeis("private"))
      isPrivate = true
      handle.increment()
    elsif(handle.safeis("var") || handle.safeis("function"))
      def current = handle.current

      if(count == 0)
        if(!isPrivate)
          handle.insert("public ")
          handle.increment()
        end

        if(isStatic)
          def position = handle.position
          handle.nextToken()
          handle.position = position

          if(!handle.safeis("new"))
            handle.insert("static ")
            handle.increment()
          end
        end
      end

      isPrivate = false
      handle.increment(current)
    else
      handle.increment()
    end
  end

  return handle.content
end
